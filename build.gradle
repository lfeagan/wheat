buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:5.+"
        classpath "com.github.evestera:plugin:0.1.0"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.24"
    }
}

plugins {
    id "edu.sc.seis.version-class" version "1.4.0"
}

repositories {
    mavenCentral()
}

ext.pluginsDir = rootProject.buildDir.path + '/plugins'

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

ext {
    // Published Projects
    publishedProjects = [
            project(':wheat-common'),
    ]
}

// Common configuration for all subprojects
// Borrowed from: https://github.com/h2oai/sparkling-water/blob/master/build.gradle
configure(subprojects) { project ->
    repositories {
        // Should be enabled only in development mode
        if (project.hasProperty('useMavenLocal') || versionQualifier.endsWith("-SNAPSHOT")) {
            mavenLocal()
        }

        mavenCentral()

        // Public Sonatype repository
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }
    }

    // Publish artifacts
    if (project in publishedProjects) {
        apply from: "$rootDir/gradle/publish.gradle"
    }
}
