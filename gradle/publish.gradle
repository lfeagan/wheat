apply plugin: 'java'
apply plugin: 'maven-publish'
//apply plugin: 'signing'

jar {
    from(rootProject.projectDir) {
        include "LICENSE"
        into "META-INF"
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

// Sonatype does not support Gradle Module Metadata
// See: https://discuss.gradle.org/t/unable-to-publish-artifact-to-mavencentral/33727/3
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/lfeagan/wheat"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = archivesBaseName
                description = project.description ?: rootProject.description
                url = 'https://github.com/lfeagan/wheat'
                organization {
                    name = "lfeagan"
                    url = "https://vectorcomputing.net"
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lfeagan'
                        name = 'Lance Feagan'
                        email = '<given_initial><family_name>@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:lfeagan/wheat'
                    developerConnection = 'scm:git@github.com:lfeagan/wheat'
                    url = 'https://github.com/lfeagan/wheat'
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/lfeagan/wheat/issues"
                }
            }
        }
    }
}

//signing {
//    if (System.getenv('SIGNING_KEY') && System.getenv('SIGNING_PASSWORD')) {
//        useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASSWORD'))
//    }
//    sign publishing.publications.mavenJava
//}
